=== init ===
{ YACK(i_started_conversation) <- NO }
-> exit


== start ===
{ YACK(i_started_conversation) <- YES }
delores: SAY(22118, "Hi!") [once]
leonard: SAY(22119, "Howdy!") [once]
-> main

== they_start ===
leonard: SAY(22120, "Hi! My name is Carla.") [once]
-> main

=== main ===
leonard: SAY(22121, "We are almost in! Are you on your own?")
1 SAY(22122,"Yes. My life has been a series of poor choices.") -> dont_get_you
2 SAY(22123,"Yes, but at least I don't talk to the people in line.") -> dont_get_you [!YACK(i_started_conversation)]
3 SAY(22124,"My friends are just a bit late.") -> advance_in_queue
4 SAY(22125,"Bye.") -> done_in_line

=== dont_get_you ===
{ YACK(dont_call_D) = YES }
leonard: SAY(22126, "I don't get what you mean, but you seem so funny!")
-> advance_in_queue

=== advance_in_queue ===
leonard: SAY(22127,"I'm also on my own. Don't worry, it's gonna be fun!")
natalie: SAY(22128,"Next!")
waitfor coroner
{
	inputOff(@{
		actorWalkTo(coroner, AStreet.postOfficeDoor)
		breakwhilewalking(coroner)
		actorAt(coroner, Diner, Diner.daveSpot1)
		sayLine(leonard, SAY(21997,"We are almost inside"))
		actorWalkTo(delores, AStreet.cuaSecondSpot)
		actorWalkTo(leonard, AStreet.cuaFirstSpot)
		breakwhilewalking(leonard)
		coroner.name = TEXT(71540,"creep")
		actorFace(leonard, DIR_LEFT)
	})
}
-> what_kind_of_person

=== what_kind_of_person ===
1 SAY(22129,"Why are you here?") -> what_type_of_person
2 SAY(22130,"What are your hobbies?") -> what_do_you_do
3 SAY(22131,"Let's skip the club and go straight to the bad sex.") -> skip_flirting [once]

=== skip_flirting ===
leonard: SAY(22132,"What?!")
delores: SAY(22133,"Ah, I was kidding.")
-> what_kind_of_person

=== what_type_of_person ===
leonard: SAY(22134,"I like to dance and have fun!")
delores: SAY(22135,"Such a unique personality.")
-> advance_again

=== what_do_you_do ===
leonard: SAY(22136,"I like nature, sport and watching Netflix!")
delores: SAY(22137,"VERY uncommon.")
-> advance_again

=== advance_again ===
delores: SAY(22138,"But don't worry, I'm also dead inside.")
natalie: SAY(22139,"Next!")
waitfor leonard
{
	inputOff(@{
		actorWalkTo(leonard, AStreet.postOfficeDoor)
		breakwhilewalking(leonard)
		actorFace(leonard, DIR_FRONT)
		sayLine(leonard, SAY(21998,"See you inside!"))
		breakwhiletalking(leonard)
		actorAt(leonard, Diner, Diner.daveSpot2)
		actorWalkTo(delores, AStreet.cuaFirstSpot)
		breakwhilewalking(delores)
		sayLine(delores, SAY(21999,"At least I had some fun while waiting in line."))
		breakwhiletalking(delores)
		sayLine(natalie, SAY(22128,"Next!"))
		leonard.name = TEXT(71222,"normie girl")
		AStreet.line_done = YES
	})
}
-> done_in_line

=== in_club ===
-> once_inside [once]
leonard: SAY(22141,"This is my favorite song!")
-> exit

=== once_inside ===
delores: SAY(22142,"Hey, you are the girl from the line!")
leonard: SAY(22143,"This is my favorite song!")
delores: SAY(22144,"Lucky you.")
delores: SAY(22145,"They play it all the time on the radio.")
delores: SAY(22146,"That's a lot of oportunities to enjoy your favorite song.")
waitfor leonard
{
	inputOff(@{
		actorFace(delores, DIR_FRONT)
		breaktime(0.5)
	})
}
delores: SAY(22147,"She is ignoring me.")
-> exit

=== done_in_line ===
waitfor leonard
{
	inputOff(@{
		actorFace(leonard, DIR_RIGHT)
	})
}
-> exit

=== done ===
-> exit

