// ----------------------------------------------------------------------------
//
//  Copyright (c) 2020 Terrible Toybox, Inc. All rights reserved.
//  Licensed for personal use only: https://github.com/grumpygamer/DeloresDev
//
// ----------------------------------------------------------------------------

=== init ===
-> exit

=== start ===
-> main

=== main ===
sandy: SAY(22242,"What can I get you?")
1 SAY(12247,"How are you doing?") -> life [once]
2 SAY(12248,"Why is there a pile of trash next to the counter?") -> trash [once]
3 SAY(12249,"I'll have a gin and tonic, please.") -> gin [once]
4 SAY(12249,"I'll have a glass of whisky.") -> whisky [once]
5 SAY(12249,"Can I have some water?") -> water [once]
9 SAY(12250,"I'm good for now, thanks.") -> done


// ------------------------------------------------------------------------------------------------
=== life ===
sandy: SAY(12251,"Can't complain.")
sandy: SAY(12253,"The boss prohibits it.")
-> main

// ------------------------------------------------------------------------------------------------
=== trash ===
sandy: SAY(12255,"The problem of reusing assets, you know.")
-> main

// ------------------------------------------------------------------------------------------------
=== gin ===
sandy: SAY(12257,"We are out of tonic.")
delores: SAY(12277,"Just the gin then.")
sandy: SAY(12257,"We are out of gin.")
-> main

=== whisky ===
sandy: SAY(12257,"There's an extra charge for whisky.")
1 SAY(12258,"That's fine.") -> whisky_confirm
2 SAY(12259,"I changed my mind.") -> main

// ------------------------------------------------------------------------------------------------
=== whisky_confirm ===
sandy: SAY(12268,"You have to pay in advance.")
2 SAY(12259,"I changed my mind.") -> main

// ------------------------------------------------------------------------------------------------
=== water ===
sandy: SAY(12274,"Sure thing! Coming right up.")
{
	objectOffsetTo(sandy, point(0,-10), 0.25 )
	water_sid <- playSoundAt(SOUNDID(water_running), sandy)
	breaktime(2.5)
	stopSound(water_sid)
	objectOffsetTo(sandy, point(0,0), 0.25 )
	breaktime(0.3)
	sayLine(sandy, SAY(12275,"Here you go."))
	playAnimation(sandy, "reach_med")
	breaktime(0.2)
	pickupObject(Inventory.glass)
	playAnimation(sandy, "stand")
}
delores: SAY(12277,"Thanks!")
-> done

=== start_first_time ===
{
	breaktime(0.1)
}
delores: SAY(12277,"Why was there a line if the place is empty?!")
{
	breakwhiletalking()
}
-> exit

=== done ===
sandy: SAY(12279,"Enjoy.")
-> exit




