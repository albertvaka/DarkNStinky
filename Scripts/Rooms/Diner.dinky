// ----------------------------------------------------------------------------
//
//  Copyright (c) 2020 Terrible Toybox, Inc. All rights reserved.
//  Licensed for personal use only: https://github.com/grumpygamer/DeloresDev
//
// ----------------------------------------------------------------------------

DEFINE_ROOM("Diner", {
	music_pool = "town"
	ambient_sound = SOUNDID(ambient_inside)

	first_time = YES

	// --------------------------------------------------------------------------------------------
	// Enter/Exit code

	enter = function(from_door=null) {
		flickerLight()

		playSoundAt(SOUNDID(hotdog_machine), hotDogMachine)

		objectState(flies, "state0")

		startthread(deloresDance)

		if (first_time) {
			startYack(delores, sandy, "Sandy:start_first_time")
			first_time = NO
		}

	}

	exit = function() {
	}

	// --------------------------------------------------------------------------------------------
	// General room functions

	script deloresDance(){
		leonard._dancingTID <- threadid()
		leonard.no_idle_animations = YES
		actorCostume(leonard, "DeloresDancingAnimation")
		actorFace(leonard, DIR_FRONT)
		do {
			playAnimation(leonard, "cabbage_start")
			breakwhileanimating(leonard)
			for (local i = 1; i <= 3; i += 1) {
				playAnimation(leonard, "cabbage_loop")
				breakwhileanimating(leonard)
			}

			for (local i = 1; i <= 2; i += 1) {
				playAnimation(leonard, "egyptian")
				breakwhileanimating(leonard)
			}
		}
	}

	function flickerLight() {
		objectState(blueNeon, "off")
		startthread(@{
			do {
				breaktime(0.01, 0.2)
				objectState(blueNeon, "on")
				roomLightEnable(1, YES)
				playSoundAt(SOUNDID(flicker_fluorescent), blueNeon)
				breaktime(1.0, 4.0)
				objectState(blueNeon, "off")
				roomLightEnable(1, NO)
			}
		})
	}


	// --------------------------------------------------------------------------------------------
	// Objects

	// --------------------------------------------------------------------------------------------
	blueNeon = {}

	// --------------------------------------------------------------------------------------------
	MainStreetDoor = DEFINE_OBJECT({
		name = TEXT(11353,"Dark'n Smelly")
		default_verb = "verb_leave"
		no_animation = YES
		VERB(leave) {
			exitRoomFromDoor(this, AStreet.postOfficeDoor)
		}
	})

	// --------------------------------------------------------------------------------------------
	dinerBackDoor = DEFINE_OBJECT({
		name = TEXT(11354,"WC")
		default_verb = "verb_enter"
		no_animation = YES
		VERB(lookat) {
			sayLine(SAY(11331,"The smell comes out even with the door closed."))
		}
		VERB(enter) {
			exitRoomFromDoor(this, Bathroom.bathroomDoor)
			
		}
	})


	// --------------------------------------------------------------------------------------------
	hotDog1 = {}
	hotDog2 = {}
	hotDog3 = {}
	hotDog4 = {}
	flies = {}

	// --------------------------------------------------------------------------------------------
	hotDogMachine = DEFINE_OBJECT({
		name = TEXT(11569,"hot dogs")
		article = ""

		VERB(lookat) {
			sayLine(SAY(11555,"The Suspiciously Green Hotdog, a house specialty."))
		}
	})


// NEW_OBJECT_HERE
})
